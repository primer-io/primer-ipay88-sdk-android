apply plugin: "maven-publish"

publishing {
    publications {
        aar(MavenPublication) {
            groupId "${GROUP}"
            artifactId "${POM_ARTIFACT_ID}"
            version "${VERSION_NAME}"
            artifact file("$buildDir/outputs/aar/ipay88-release.aar")
            pom.withXml {
                // for dependencies and exclusions
                def dependenciesNode = asNode().appendNode('dependencies')
                def configurationNames = ["implementation", "api", "runtimeOnly"]
                configurationNames.each { confName ->
                    configurations[confName].allDependencies.each {
                        if (it.group != null && it.name != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            if (it.hasProperty('excludeRules') && it.excludeRules.size() > 0) {
                                def exclusionsNode = dependencyNode.appendNode('exclusions')
                                it.excludeRules.each { rule ->
                                    def exclusionNode = exclusionsNode.appendNode('exclusion')
                                    exclusionNode.appendNode('groupId', rule.group)
                                    exclusionNode.appendNode('artifactId', rule.module)
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

assemble.finalizedBy(artifactoryPublish)

artifactory {
    contextUrl = "${ARTIFACTORY_CONTEXT_URL}"
    publish {
        repository {
            repoKey = "${ARTIFACTORY_REPO_KEY}"
            username = System.getenv("PRIMER_ANDROID_ARTIFACTORY_DEPLOY_USERNAME")
            password = System.getenv("PRIMER_ANDROID_ARTIFACTORY_DEPLOY_PASSWORD")
            maven = true
        }
        defaults {
            publications(publishing.publications.aar)
            publishArtifacts = true
        }
    }
    resolve {
        repository {
            repoKey = "${ARTIFACTORY_REPO_KEY}"
            username = System.getenv("PRIMER_ANDROID_ARTIFACTORY_DEPLOY_USERNAME")
            password = System.getenv("PRIMER_ANDROID_ARTIFACTORY_DEPLOY_PASSWORD")
            maven = true
        }
    }
}
